resource "aws_s3_bucket" "terraform_state" {
  count  = var.s3_bucket_name != "" ? 0 : 1
  bucket = "${var.project_name}-terraform-state-${data.aws_caller_identity.current.account_id}"

  tags = merge(
    local.common_tags,
    {
      Name = "${local.cluster_name}-terraform-state"
    }
  )
}

resource "aws_s3_bucket_versioning" "terraform_state" {
  count  = var.s3_bucket_name != "" ? 0 : 1
  bucket = aws_s3_bucket.terraform_state[0].id

  versioning_configuration {
    status = "Enabled"
  }
}

resource "aws_s3_bucket_server_side_encryption_configuration" "terraform_state" {
  count  = var.s3_bucket_name != "" ? 0 : 1
  bucket = aws_s3_bucket.terraform_state[0].id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm = "AES256"
    }
  }
}

resource "aws_s3_bucket_public_access_block" "terraform_state" {
  count  = var.s3_bucket_name != "" ? 0 : 1
  bucket = aws_s3_bucket.terraform_state[0].id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_dynamodb_table" "terraform_state_lock" {
  count        = var.dynamodb_table_name == "" ? 1 : 0
  name         = "${var.project_name}-terraform-state-lock-${data.aws_caller_identity.current.account_id}"
  billing_mode = "PAY_PER_REQUEST"
  hash_key     = "LockID"

  attribute {
    name = "LockID"
    type = "S"
  }

  tags = merge(
    local.common_tags,
    {
      Name = "${local.cluster_name}-terraform-state-lock"
    }
  )
}

